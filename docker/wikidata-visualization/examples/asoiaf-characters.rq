# A Song of Ice and Fire character relationships

SELECT DISTINCT
?personItem ?personName ?gender ?family ?relativeItem ?relativeName ?relativeType
WHERE {
  VALUES ?relativeProp { wdt:P22 wdt:P25 wdt:P3373 wdt:P26 wdt:P451 wdt:P40 }
  ?personItem wdt:P31 wd:Q20086260 ;
              ?relativeProp ?relativeItem .
  OPTIONAL { ?personItem wdt:P21 ?genderItem . }
  OPTIONAL { ?personItem wdt:P53 ?familyItem . }
  ?relativeTypeWd wikibase:directClaim ?relativeProp .
  SERVICE wikibase:label { bd:serviceParam wikibase:language "en".
                           ?personItem rdfs:label ?personName .
                           ?genderItem rdfs:label ?gender .
                           ?familyItem rdfs:label ?family .
                           ?relativeItem rdfs:label ?relativeName .
                           ?relativeTypeWd rdfs:label ?relativeType . }
}
