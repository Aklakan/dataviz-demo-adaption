# Craters on the Moon named after French people

SELECT
?craterItem ?crater ?image ?namedAfterItem ?namedAfter ?coord ?diameterInKM ?depthInM
WHERE {
  ?craterItem wdt:P31 wd:Q55818 ; # impact crater
              wdt:P376 wd:Q405 ; # Moon
              wdt:P138 ?namedAfterItem .
  ?namedAfterItem wdt:P27 wd:Q142 .
  OPTIONAL { ?craterItem wdt:P18 ?image . }
  OPTIONAL { ?craterItem wdt:P625 ?coord . }
  OPTIONAL { ?craterItem wdt:P2386 ?diameterInKM . }
  OPTIONAL { ?craterItem wdt:P4511 ?depthInM . }
  OPTIONAL { ?craterItem rdfs:label ?crater .
             FILTER( LANG(?crater) = "en" ) . }
  OPTIONAL { ?namedAfterItem rdfs:label ?namedAfter .
             FILTER( LANG(?namedAfter) = "fr" ) . }
}
