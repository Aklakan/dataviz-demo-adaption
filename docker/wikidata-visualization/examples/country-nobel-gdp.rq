# Number of Nobel laureates and GDP by country

SELECT
?countryItem ?countryName
# number of Nobel laureates
(COUNT (*) AS ?numOfLaureates)
# number of Nobel laureates per capita
(COUNT(*) / AVG(?population) AS ?numOfLaureatesPerCapita)
# GDP
(AVG(?GDP) AS ?GDP)
# GDP per capita
(AVG(?GDPPerCapita) AS ?GDPPerCapita)
?countryCoord
WHERE {
  ?personItem wdt:P31 wd:Q5 ; # human
              wdt:P166/wdt:P31 wd:Q7191 ; # Nobel Prize
              wdt:P27 ?countryItem .
  ?countryItem wdt:P2131 ?GDP;
               wdt:P2132 ?GDPPerCapita ;
               wdt:P1082 ?population .
  OPTIONAL { ?countryItem wdt:P625 ?countryCoord . }
  SERVICE wikibase:label { bd:serviceParam wikibase:language "en" .
                           ?countryItem rdfs:label ?countryName }
}
GROUP BY ?countryItem ?countryName ?countryCoord
